buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		maven { url = 'https://repo.spongepowered.org/maven' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

archivesBaseName = project.name + '-' + project.mcVersion

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

mixin {
	add sourceSets.main, "${project.name.toLowerCase()}.refmap.json"
}

minecraft {
	mappings channel: 'official', version: project.mcVersion
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	runs {
		client = {
			properties 'forge.logging.markers': ''
			properties 'forge.logging.console.level': 'debug'
			properties 'terminal.ansi': 'true'
			workingDirectory project.file('run').canonicalPath
			source sourceSets.main

			arg "-mixin.debug=true"
			arg "-mixin.checks.interfaces=true"
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
		}
	}
}

repositories {
	maven {
		name = "CurseMaven"
		url "https://www.cursemaven.com"
	}
}

sourceSets {
	main {
		resources {
			srcDir 'src/generated/resources'
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${project.mcVersion}-${project.forgeVersion}"
	annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'

	implementation fg.deobf('curse.maven:iceberg-520110:3439178')
	implementation fg.deobf('curse.maven:placebo-283644:3437009')
	implementation fg.deobf('curse.maven:toastcontrol-271740:3172881')
	implementation fg.deobf('curse.maven:jade-324717:3302544')
	implementation fg.deobf('curse.maven:advancementplaques-499826:3507674')

	// FTB Quests support
	implementation fg.deobf('curse.maven:architectury-419699:3782883')
	implementation fg.deobf('curse.maven:ftblibrary-404465:3553840')
	implementation fg.deobf('curse.maven:ftbquests-289412:3672855')

	// Custom Quests support
	implementation fg.deobf('curse.maven:customquests-334985:3636451')
}

jar {
	manifest {
		attributes(["Specification-Title": project.name,
					"Specification-Vendor": project.author,
					"Specification-Version": project.version,
					"Implementation-Title": project.name,
					"Implementation-Version": project.version,
					"Implementation-Vendor" : project.author,
					"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
					"MixinConfigs": "${project.name.toLowerCase()}.mixins.json"],)
	}
}
